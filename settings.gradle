import org.gradle.api.initialization.resolve.RepositoriesMode

pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        google()
    }
    resolutionStrategy {
        eachPlugin {
            def pluginId = it.requested.id.id
            if (pluginId == "com.likethesalad.artifact-publisher") {
                useModule("com.likethesalad.tools:artifact-publisher:${it.requested.version}")
            }
            if (pluginId == "plugin-metadata-producer") {
                useModule("com.likethesalad.tools:plugin-metadata-producer:1.0.0")
            }
            if (it.requested.id.namespace == "com.likethesalad.tools") {
                useModule("com.likethesalad.tools:plugin-tools:${it.requested.version}")
            }
        }
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
}
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:2.1.10"
    }
}
rootProject.name = 'stem'
include ':stem-plugin'
include ':templates-provider-plugin'
include ':templates-provider-api'
include ':common-tools'